#alias {serv %1}{
    #chat message 1 %1;
}

#alias {chat_connect}{
    #chat uninitialize;
    #chat initialize;
    #chat name ohr;
    #chat call chat.mikejsavage.co.uk 4050;
}

#var current_hit_points 0;
#var current_hit_points_color {};

#var current_mana 0;
#var current_mana_color {};

#var colors {
    {0} {<118>}
    {1} {<138>}
    {2} {<128>}
    {3} {<148>}
    {4} {<148>}
};

#action {%1[[ %2/%3 | %4/%5 | %6 | %7 | %8 ]][%9]}{

    #var current_hit_points {};
    #var current_hit_points {%2};
    #math current_hit_points_idx {((%2*100)/%3) / 25};
    #var current_hit_points_color {};
    #var current_hit_points_color {$colors[$current_hit_points_idx]$current_hit_points};

    #var current_mana {};
    #var current_mana {%4};
    #math current_mana_idx {((%4*100)/%5) / 25};
    #var current_mana_color {};
    #var current_mana_color {$colors[$current_mana_idx]$current_mana};

};

#var combat_rep_enabled true;
#var spin_count 0;
#list good_words create {Woop!} {Snap!} {Huzzah!} {Whoopee!} {Whoa!};


#function {do_combatrep}{

    #if {"%1" == "enable"}{
        #if {"$combat_rep_enabled" == "false"}{
            #var message {<138>[[<088> <148>Combat spam now<088> <128>ENABLED<088> <138>]]<088>};
            #chat {emote} {all} {$message};
            #var combat_rep_enabled {};
            #var combat_rep_enabled true;
        };
    }{

    #if {"%1" == "enableq"}{
        #if {"$combat_rep_enabled" == "false"}{
            #var message {<138>[[<088> <148>Combat spam now<088> <128>ENABLED<088> <138>]]<088>};
            #showme $message;
            #var combat_rep_enabled {};
            #var combat_rep_enabled true;
        };
    }{

    #if {"%1" == "disable"}{
        #if {"$combat_rep_enabled" == "true"}{
            #var message {<138>[[<088> <148>Combat spam now<088> <118>DISABLED<088> <138>]]<088>};
            #chat {emote} {all} {$message};
            #var combat_rep_enabled {};
            #var combat_rep_enabled false;
        };
    }{

    #if {"%1" == "disableq"}{
        #if {"$combat_rep_enabled" == "true"}{
            #var message {<138>[[<088> <148>Combat spam now<088> <118>DISABLED<088> <138>]]<088>};
            #showme $message;
            #var combat_rep_enabled {};
            #var combat_rep_enabled false;
        };
    }{

    #if {"%1" == "good"}{
        #if {"$combat_rep_enabled" == "true"}{
            #math word_idx 1d&good_words[];
            #chat {emote} {all} {<138>[[<088> <148>$good_words[$word_idx]<088> <138>|<088> %2 <138>]]<088>};
        };
    }{

    #if {"%1" == "bad"}{
        #if {"$combat_rep_enabled" == "true"}{
            #chat {emote} {all} {<138>[[<088> <148>Dang!<088> <138>|<088> %2 <138>]]<088>};
        };
    }{

    #if {"%1" == "other"}{
        #if {"$combat_rep_enabled" == "true"}{
            #chat {emote} {all} {<138>[[<088> %2 <138>]]<088>};
        };
    };

    };};};};};};

    #var result {#no};

};

#action {You step into the portal, and the world spins madly...} {
    #math new_count {$spin_count + 1};
    #var spin_count {};
    #var spin_count $new_count;
    #showme {Spin count: $spin_count};
}

#action {You step into the portal and the world fades into the mist.}{
    #var spin_count {};
    #var spin_count 0;
}

#no #action {The white aura around your body fades.} {;
#no     @do_combatrep{bad; <148>My <178>SANC<148> just faded!<088>};
#no };

#action {%1 chokes and utters some muffled noises.} {
    @do_combatrep{good; <148>%1 just got <158>PLAGUED<148> in the face!<088>};
};

#action {%1 chokes and gasps for breath.} {
    @do_combatrep{good; <148>%1 is <128>MUFFLED<148> like a chump!<088>};
};

#alias {cc %1} {
    #chat {emote} {all} {<138>[[<088> $current_hit_points_color <138>| $current_mana_color <138>]] <128>:: <178>%1 <128>::<088>};
};

#alias {combatrep %1}{
    @do_combatrep{%1};
};

