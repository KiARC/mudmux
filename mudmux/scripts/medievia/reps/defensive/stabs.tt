#no mudmux module medievia/reps/defensive/stabs.tt
@_module{medievia/reps/defensive/stabs};

@_require{medievia/chat};
@_require{medievia/splitprompt};
@_require{medievia/room_reader};

#no todo: add colors for stab strengths
#no todo: add stabbed to death and attempted to stab


#list stab_strengths create;
#var stabbed_dead 0;
#var twist_present 0;

#alias clear_stab_data {
    #list stab_strengths clear;
    #var stabbed_dead 0;
    #var twist_present 0;
};

#alias update_stab_trigger {
    #var prompt_rep_trigger {||| STABBED TRIGGER:: %1};
    #list stab_strengths add %2;
    #var stabbed_dead %3;
};

#function stab_strength_string {
    #var result {};
    #list stab_strengths size num_attempts;
    #if {$num_attempts > 0}{
        #var result {<178>(};
        #foreach $stab_strengths[] i {
            #if {$i > 1}{#var result {${result}|}};
            #var result {${result}$stab_strengths[$i]};
        };
        #if {$twist_present} {
            #var result{${result}<118>T};
        };
        #var result {${result}<178>) };
    };
};

#action {%1 slashes at you with %2 and misses.} {update_stab_trigger {%1} {0.0} {0}};
#action {You hear footfalls behind, turning your head, you notice %1.}  {update_stab_trigger {%1} {0.0} {0}};
#action {Instinctively, you jump to the side and avoid %1 as %2 lunges at your back.} {update_stab_trigger {%1} {0.0} {0}};

#action {You wince in pain as %1 clumsily stabs at your back.} {update_stab_trigger {%1} {1.5} {0}};
#action {You feel a sharp, stinging pain as %1 stabs at your shoulder blades with %2.} {update_stab_trigger {%1} {2.0} {0}};
#action {You reel in pain as %1 lunges at your back and lazily penetrates your defenses with %2.} {update_stab_trigger {%1} {2.5} {0}};
#action {Tendrils of pain shoot through your back as %1 stabs at your back with %2 and lands an off-center blow} {update_stab_trigger {%1} {3.0} {0}};
#action {A vicious backstab from %1 causes you to choke on your own blood.}  {update_stab_trigger {%1} {3.5} {0}};
#action {You convulse in pain as %1 powerfully stabs you in the back with %2} {update_stab_trigger {%1} {4.0} {0}};
#action {%1 skillfully stabs at your back with %2 and tears through your %3, landing a devastating backstab.} {update_stab_trigger {%1} {4.5} {0}};
#action {Pain shoots through your back as %1 stabs at you with %2 and slides it through your defenses.} {update_stab_trigger {%1} {5.0} {0}};
#action {%1 critically stabs you in the back with %2 and your body is wracked with excruciating pain.} {update_stab_trigger {%1} {5.5} {0}};
#action {%1 stabs you in the back critically with %2 and your body is wracked with excruciating pain.} {update_stab_trigger {%1} {5.5} {0}};

#action {Suddenly %1 stabs you in the back, RIP...} {update_stab_trigger {%1} {6.0} {1}};

#no #action {%1's assault slices open your thigh. Your leg gives out and you crumple under your own weight.} {update_stab_trigger {%1} {?} {0}};

#action {%1 twists %2 as %3 removes it from your back.} {
    #var twist_present 1;
};

#action {||| STABBED TRIGGER:: %1} {

    #math stab_dmg {$prev_hit_points - $current_hit_points};

    #var pk_string {};
    #if {"$pkness" == "NPK"} {#var pk_string {<138> | NPK<088>};};
    #if {"$pkness" == "CPK"} {#var pk_string {<138> | <118>CPK<088>};};

    @do_combatrep{vbad; <148>I just got <138>STABBED<148> @stab_strength_string{}<148>right in the back by <168>%1<148> for <118>$stab_dmg<148> damage to ${current_hit_points_color}<148> hp!$pk_string<088>};

    clear_stab_data;
    #line gag;

};